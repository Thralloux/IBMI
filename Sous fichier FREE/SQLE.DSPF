     A* *===============================================================
     A* *                      SOUS FICHIER
     A* *===============================================================
     A                                      INDARA
     A          R FE                        SFL
     A            OPT            1A  I  9  2
     A            ZON1          10A  O  9  4
     A            ZON2          10A  O  9 15
     A            ZON3          10Y 0O  9 26
     A                                      EDTCDE(4)
     A            ZON4          10Y 0O  9 37
     A                                      EDTCDE(4)
     A* *====================  CONTROLE  ===============================
     A          R FC                        SFLCTL(FE)
     A                                      CF03(03)
     A                                      OVERLAY
     A  32                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  90                                  SFLEND(*MORE)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A            RANG           4S 0H      SFLRCDNBR
     A* *====================  USER & DATE  ============================
     A                                  1  3SYSNAME
     A                                  2  3USER
     A                                  2 71TIME
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A* *====================  TITRE  ===================================
     A                                  1 34'Sous Fichier FREE'
     A                                      COLOR(WHT)
     A* *====================  OPTION  ===================================
     A                                  5  2'Indiquez vos options, puis appuyez-
     A                                       sur ENTREE.'
     A                                      COLOR(BLU)
     A                                  6  3'2=Editer'
     A                                      COLOR(BLU)
     A                                  6 14'4=Supprimer'
     A                                      COLOR(BLU)
     A                                  6 28'5=Afficher'
     A                                      COLOR(BLU)
     A* *====================  ENTETE  ===================================
     A                                  8  4'Nom_Pays'
     A                                      COLOR(WHT)
     A                                  8 15'Capitale'
     A                                      COLOR(WHT)
     A                                  8 26'Population'
     A                                      COLOR(WHT)
     A                                  8 37'Surface'
     A                                      COLOR(WHT)
     A* *====================  BOTTOM  =================================
     A          R FB
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A* *====================  BOTTOM  =================================
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            $PGM                      SFLPGMQ(10)
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      HELP
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            $PGM                      SFLPGMQ(10)
     A* *===============================================================
     A* *                      FORMAT 1
     A* *===============================================================
     A          R FD
     A                                      CF12(12)
     A* *====================  USER & DATE  ============================
     A                                  1  3SYSNAME
     A                                  2  3USER
     A                                  2 71TIME
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A* *====================  TITRE  ===================================
     A            TLT           20A  O  1 34COLOR(WHT)
     A* *====================  MAIN  ===================================
     A                                  7  3'Pays'
     A                                  7 17':'
     A                                  8  3'Capital'
     A                                  8 17':'
     A                                  9  3'Population'
     A                                  9 17':'
     A                                 10  3'Surface'
     A                                 10 17':'
     A            ENOMPA    R        B  7 20REFFLD(PAYSF/NOMPAY NBOUVIER/PAYS)
     A  91                                  DSPATR(PR)
     A            ECAPIT    R        B  8 20REFFLD(PAYSF/CAPITA NBOUVIER/PAYS)
     A  91                                  DSPATR(PR)
     A            EPOPUL    R        B  9 20REFFLD(PAYSF/POPULA NBOUVIER/PAYS)
     A  91                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            ESURFA    R        B 10 20REFFLD(PAYSF/SURFAC NBOUVIER/PAYS)
     A  91                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A* *====================  BOTTOM  =================================
     A                                 23 14'F12=Annuler'
     A                                      COLOR(BLU)
     A* *====================  FENETRE =================================
     A          R BIDON                     ASSUME
     A                                  1  2' '
     ****************
     A          R FV
     A                                      WINDOW(10 20 5 50)
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      CF10(10)
     A                                      WDWBORDER((*COLOR PNK) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                  1 12'Confirmation de la modification'
     A                                      DSPATR(RI)
     A                                  4 33'F03=Annuler'
     A                                      COLOR(BLU)
     A                                  4  7'F10=Valider'
     A                                      COLOR(BLU)
     A* *===============================================================
     A* *                      NETTOYAGE
     A* *===============================================================
     A          R FN
